buildscript {
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:$versions.spring_boot")
        classpath("se.transmode.gradle:gradle-docker:${dockerPluginsVersion}")
    }
}
apply plugin: "org.springframework.boot"
apply plugin: "io.spring.dependency-management"
apply plugin: "docker"

dependencyManagement {
    imports {
        mavenBom "org.springframework.boot:spring-boot-dependencies:$versions.spring_boot"
    }
}

docker {
    baseImage "java:8-jre"
    maintainer "zhangbin zhangbin1010@qq.com"
}

task dockerBuilder(type: Docker, dependsOn: build) {
    applicationName = jar.baseName
    tagVersion = jar.version
    tag = "${project.group}/${applicationName}"
    volume('/tmp')
    addFile("${jar.archiveName}", "/app/${jar.archiveName}")
    entryPoint(["java", "-Xmx500m", "-Djava.security.egd=file:/dev/./urandom", "-jar", "/app/${jar.archiveName}"])
    exposePort(9090)
    doFirst {
        copy {
            from jar
            into stageDir
        }
    }
}

dependencies {
    compile project(":acp:acp-spring-boot-starter-common")

    compile "org.springframework.boot:spring-boot-starter-data-jpa"

    testCompile("org.springframework.boot:spring-boot-starter-test") {
        exclude group: 'junit'
    }
}
